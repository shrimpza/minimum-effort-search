plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'jacoco'

	id 'org.gradlex.extra-java-module-info' version '1.13.1'
	id 'org.beryx.jlink' version '3.1.3'
}

group = 'net.shrimpworks'
version = "1.9"
if (System.getenv().containsKey("BUILD_NUMBER")) {
	version += ".${System.env.BUILD_NUMBER}"
} else version += ".DEV"

application {
	mainClass = 'net.shrimpworks.mes.Main'
	mainModule = 'shrimpworks.mes'
}

jar {
	manifest {
		attributes(
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Main-Class': application.mainClass,
		)
	}
}

compileJava {
	options.release = 21
}

jlink {
	mergedModuleName = "shrimpworks.mes.merged"
	options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages']

	imageName = "${project.name}"
	imageDir = layout.buildDirectory.dir(project.name)

	jpackage {
		installerType = project.findProperty('installerType')
	}
}

def tarTask = tasks.register('jlinkTar', Tar) {
	dependsOn jlinkZip
	archiveFileName = "${jlink.imageName.get()}.tgz"
	destinationDirectory = layout.buildDirectory
	compression = Compression.GZIP

	into("${jlink.imageName.get()}") {
		from jlink.imageDir
	}
}

repositories {
	mavenCentral()
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://git.pookaroo.au/api/packages/shrimp/maven'
			credentials(HttpHeaderCredentials) {
				name = "Authorization"
				value = "token ${System.env.PACKAGES_TOKEN}"
			}

			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
			artifact(tarTask) {
				classifier = 'bin'
				extension = 'tgz'
			}
		}
	}
}

artifacts {
	archives(layout.buildDirectory.file("${project.name}-bin.tgz")) {
		type = 'tgz'
		builtBy tarTask
	}
}

dependencies {
	implementation 'redis.clients:jedis:7.2.1'
	implementation 'io.undertow:undertow-core:2.3.22.Final'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'

	implementation 'org.slf4j:slf4j-api:2.0.17'
	implementation 'org.slf4j:slf4j-simple:2.0.17'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
}

extraJavaModuleInfo {
	failOnMissingModuleInfo.set(false)

	module("org.jboss.xnio:xnio-api", "xnio.api") {
		exports("org.xnio")
	}

	module("io.undertow:undertow-core", "undertow.core") {
		requires("xnio.api")

		exports("io.undertow")
		exports("io.undertow.io")
		exports("io.undertow.predicate")
		exports("io.undertow.server")
		exports("io.undertow.server.handlers")
		exports("io.undertow.server.handlers.encoding")
		exports("io.undertow.util")
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
