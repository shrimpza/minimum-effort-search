plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'jacoco'

	id 'org.gradlex.extra-java-module-info' version '1.9'
	id 'org.beryx.jlink' version '3.1.1'
}

group = 'net.shrimpworks'
version = "1.8"
if (System.getenv().containsKey("BUILD_NUMBER")) {
	version += ".${System.env.BUILD_NUMBER}"
} else version += ".DEV"

application {
	mainClassName = 'net.shrimpworks.mes.Main'
	mainModule = 'shrimpworks.mes'
}

compileJava {
	options.release = 21
}

jlink {
	mergedModuleName = "shrimpworks.mes.merged"
	options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages']

	imageName = "${project.name}"
	imageDir = layout.buildDirectory.dir(project.name)

	jpackage {
		installerType = project.findProperty('installerType')
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://code.shrimpworks.za.net/artefacts"
	}
}

def tarBinary = artifacts.add('archives', layout.buildDirectory.file("${project.name}.tgz").get().asFile) {
	type 'tgz'
	builtBy 'jlinkTar'
}

tasks.register('jlinkTar', Tar) {
	dependsOn jlinkZip
	archiveFileName = "${jlink.imageName.get()}.tgz"
	destinationDirectory = layout.buildDirectory
	compression = Compression.GZIP

	into("${jlink.imageName.get()}") {
		from jlink.imageDir
	}
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://code.shrimpworks.za.net/artefacts/'
			authentication {
				basic(BasicAuthentication)
			}
			credentials(PasswordCredentials) {
				username = "${System.env.ARTEFACTS_USER}"
				password = "${System.env.ARTEFACTS_PASSWORD}"
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
		}
		mavenLatest(MavenPublication) {
			pom {
				version = "latest"
			}
			artifact tarBinary
		}
	}
}

dependencies {
	implementation 'redis.clients:jedis:5.2.0'
	implementation 'io.undertow:undertow-core:2.3.18.Final'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'

	implementation 'org.slf4j:slf4j-api:2.0.16'
	implementation 'org.slf4j:slf4j-simple:2.0.16'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
}

extraJavaModuleInfo {
	failOnMissingModuleInfo.set(false)

	module("org.jboss.xnio:xnio-api", "xnio.api") {
		exports("org.xnio")
	}

	module("io.undertow:undertow-core", "undertow.core") {
		requires("xnio.api")

		exports("io.undertow")
		exports("io.undertow.io")
		exports("io.undertow.predicate")
		exports("io.undertow.server")
		exports("io.undertow.server.handlers")
		exports("io.undertow.server.handlers.encoding")
		exports("io.undertow.util")
	}
}

jar {
	manifest {
		attributes(
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Main-Class': mainClassName,
		)
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
