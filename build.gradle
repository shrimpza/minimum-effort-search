plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'jacoco'
}

group = 'net.shrimpworks'
version = "1.5"

mainClassName = 'net.shrimpworks.mes.Main'

compileJava {
	options.release = 17
}

if (System.getenv().containsKey("DRONE_BUILD_NUMBER")) {
	version = "${version}.${System.env.DRONE_BUILD_NUMBER}"
}

repositories {
	mavenCentral()
	maven {
		url "https://code.shrimpworks.za.net/artefacts"
	}
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://code.shrimpworks.za.net/artefacts/'
			authentication {
				basic(BasicAuthentication)
			}
			credentials(PasswordCredentials) {
				username = "${System.env.ARTEFACTS_USER}"
				password = "${System.env.ARTEFACTS_PASSWORD}"
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
}

dependencies {
	implementation 'redis.clients:jedis:4.4.0'
	implementation 'io.undertow:undertow-core:2.3.4.Final'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2'

	implementation 'org.slf4j:slf4j-api:2.0.5'
	implementation 'org.slf4j:slf4j-simple:2.0.5'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

jar {
	manifest {
		attributes(
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Main-Class': mainClassName,
		)
	}
}

task execJar(type: Jar) {
	// exclude jar signatures - else it invalidates our fat jar
	exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	archiveClassifier = "exec"
	archiveFileName = "${archiveBaseName.get()}-${archiveClassifier.get()}.${archiveExtension.get()}"
	from sourceSets.main.output

	dependsOn configurations.runtimeClasspath

	manifest {
		attributes(
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Main-Class': mainClassName,
			'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
		)
	}

	// build the fat executable jar file
	from {
		configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
